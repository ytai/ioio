buildscript {
    ext.kotlin_version = '1.4.0'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group = 'com.github.ytai.ioio'
    version = 5.07
    ext.versionNamespace = 'IOIO'
    ext.versionString = sprintf("%s%02d%02d",
            versionNamespace,
            (int) version,
            ((int) (version * 100)) % 100)
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'signing'

    repositories {
        google()
        jcenter()
        maven {
            url "http://www.sparetimelabs.com/maven2"
        }
        maven {
            url 'https://oss.sonatype.org/content/groups/staging'
        }
    }
}

static def getTag() {
    def tagVersion = "$System.env.TAG_VERSION"
    if (tagVersion == "null") {
        println "No variable TAG_VERSION specified, it seems it runs locally, I'm using 'git describe --tags'"

        // with local un-commited changes a -DIRTY is added
        def processChanges = "git diff-index --name-only HEAD --".execute()
        def dirty = ""
        if (!processChanges.text.toString().trim().isEmpty())
            dirty = "-DIRTY"

        def process = "git describe --tags".execute()
        tagVersion = process.text.toString().trim() + dirty
    } else {
        def tagVersionToken = tagVersion.split("/")
        tagVersion = tagVersionToken[2]
    }
    return tagVersion
}

static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}
