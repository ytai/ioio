buildscript {
    ext.kotlin_version = '1.7.0'
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.hannesa2:gradle-one-jar:1.3"
    }
}

allprojects {
    group = 'com.github.ioio'
    version = getTag()
    ext.versionNamespace = 'IOIO'
    ext.versionString = sprintf("%s%s", versionNamespace, version)
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        google()
        mavenCentral()
        maven {
            url "http://www.sparetimelabs.com/maven2"
            allowInsecureProtocol = true
        }
        maven { url 'https://jitpack.io' }
    }
}

static def getTag() {
    def tagVersion = "$System.env.VERSION"
    if (tagVersion == "null") {
        // with local un-committed changes a -DIRTY is added
        def processChanges = "git diff-index --name-only HEAD --".execute()
        def dirty = ""
        if (!processChanges.text.toString().trim().isEmpty())
            dirty = "-DIRTY"

        def process = "git describe --tags".execute()
        tagVersion = process.text.toString().trim() + dirty
    } else {
        def tagVersionToken = tagVersion.split("/")
        if (tagVersionToken.size() > 2)
            tagVersion = tagVersionToken[2]
        else
            tagVersion = tagVersionToken[0]
    }
    return tagVersion
}

@SuppressWarnings('unused')
static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}
