plugins {
    id 'java'
    id 'maven-publish'
}

tasks.register('generateVersionFile', GenerateVersion) {
    version = versionString
    outputFile = file("$project.buildDir/classes/main/version.properties")
}

tasks.classes.dependsOn generateVersionFile

javadoc {
    source = fileTree(dir: 'src/main/java').matching { include 'ioio/lib/api/**' }
    failOnError = false
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

class GenerateVersion extends DefaultTask {
    @Input
    String version

    @OutputFile
    File outputFile

    @TaskAction
    void generate() {
        def file = getOutputFile()
        def properties = new Properties()
        properties.setProperty("version", version)
        properties.store(file.newWriter(), null)
    }
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.findByName("release")
            }
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
